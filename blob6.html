<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="icon" href="favicon.ico" type="image/x-icon" />      
  <title>Meta blobs - Blob #6</title>
  <link rel="stylesheet" href="blob.css" type="text/css">
</head>
<body>
  <div id="panel">
    <p>#6</p>
    <a href="index.html">
<svg width="35" height="35" version="1.1" viewBox="0 0 13.229166 13.229167" xmlns="http://www.w3.org/2000/svg"><g transform="translate(0 -283.77)"><path transform="matrix(.26458 0 0 .26458 0 283.77)" d="m25 5l-20 15v25h5v-23l15-11 15 11v18h-15v-10h5v-5h-10v15h-5v5h25 5v-25l-20-15z" style="paint-order:normal"/></g></svg>
    </a>
    <a href="https://github.com/jblanper" target="_blank">
<svg width="35" height="35" version="1.1" viewBox="0 0 13.229166 13.229167" xmlns="http://www.w3.org/2000/svg"><g transform="translate(0 -283.77)"><path d="m6.6208 286.68-1.2972 7.4083h1.3656l1.2968-7.4083z" style="paint-order:normal"/><path d="m4.433 287.69c-1.3008 0.75108-2.6017 1.502-3.9026 2.2531v0.58497c-0.013266 0.11065 0.018061 0.24834 0.004134 0.30954 1.2995 0.75025 2.599 1.5003 3.8985 2.2505v-1.4769c-0.70558-0.40737-1.4111-0.81477-2.1167-1.2222 0.70558-0.40737 1.4111-0.81478 2.1167-1.2221z" style="paint-order:normal"/><path d="m8.794 287.69c1.3008 0.75108 2.6017 1.502 3.9026 2.2531v0.58497c0.01327 0.11065-0.01806 0.24834-0.0041 0.30954-1.2995 0.75025-2.599 1.5003-3.8985 2.2505v-1.4769c0.70558-0.40737 1.4111-0.81477 2.1167-1.2222-0.70558-0.40737-1.4111-0.81478-2.1167-1.2221z" style="paint-order:normal"/></g></svg>
    </a>
  </div>
  <canvas></canvas>

  <script id="vs" type="notjs">
attribute vec4 position;

void main() {
  gl_Position = position;
}
  </script>

  <script id="fs" type="notjs">
precision mediump float;
uniform float time;
uniform vec2 resolution;

#define MAX_STEPS 255
#define MAX_DIST 100.
#define EPSILON 1.

mat2 rotate(float a) {
  return mat2(cos(a), -sin(a), sin(a), cos(a));
}

float smin( float a, float b, float k ) {
  // http://www.iquilezles.org/www/articles/smin/smin.htm
  float h = max( k-abs(a-b), 0.0 )/k;
  return min( a, b ) - h*h*h*k*(1.0/6.0);
}

float sphereSDF (vec3 p, float r) {
  return length(p) - r;
}

float displacement(vec3 p, float n) {
  return sin(p.x * n) * sin(p.y * n) * sin(p.z * n);
}

float sceneSDF (vec3 p) {
  float s1 = sphereSDF(p - vec3(-.8 * sin(time) * .2, .3, -.10 * cos(time) * .5), .125 + sin(cos(dot(p, vec3(p.y)) * 2.2 - time * .3) * 18. + time * .2) * .5 + .5);
  float s2 = sphereSDF(p - vec3(-.3, -.3, -.8), 1.);
  float s3 = sphereSDF(p - vec3(-.8 * sin(time) * .2, .5, -.10 * cos(time) * .5) * sin(length(p * sin(time * .01)) * 9. + cos(time)), 1.2);

  return smin(s1 * 1.1, s3 * .9 + displacement(p - cos(atan(p.y, p.z) - time * .001) * .5 + .5, 5.), 1.2);
}

vec3 getNormal (vec3 p) {
  float d = sceneSDF(p);
  vec2 e = vec2(.01, 0.);

  return normalize(d - vec3(
    sceneSDF(p - e.xyy),
    sceneSDF(p - e.yxy),
    sceneSDF(p - e.yyx)));
}

float raymarch (vec3 ro, vec3 rd) {
  float depth = 0.;
  for (int i = 0; i < MAX_STEPS; i++) {
    float dist = sceneSDF(ro + rd * depth);
    if (dist < EPSILON) return depth;
    depth += dist;
    if (depth >= MAX_DIST) return 0.;
  }
  return 0.;
}

float getLight (vec3 lightPos, vec3 p) {
  vec3 light = normalize(lightPos - p);
  vec3 normal = getNormal(p);

  float diff = clamp(dot(normal, light), 0., 1.);

  return diff;
}

vec3 getRayDir (vec2 uv, vec3 rayOrigin, vec3 lookat, float zoom) {
  // https://www.youtube.com/watch?v=PBxuVlp7nuM
  vec3 forward = normalize(lookat - rayOrigin);
  vec3 right = normalize(cross(vec3(0., 1., 0.), forward));
  vec3 up = cross(forward, right);
  vec3 center = rayOrigin + forward * zoom;
  vec3 intersection = center + uv.x * right + uv.y * up;
  return normalize(intersection - rayOrigin);
}

void main (void) {
  vec2 uv = (gl_FragCoord.xy / resolution.xy) * 2.0 - 1.0;
  uv.x *= resolution.x / resolution.y;

  // camera
  vec3 ro = vec3(-.3, 1., 8.);
  ro.xz *= rotate(time * .02);
  vec3 rd = getRayDir(uv, ro, vec3(0.), 2.1);

  vec3 color = vec3(0.);
  float d = raymarch(ro, rd);
  vec3 p = ro + rd * d;

  if (d > 0.) {
    vec3 lightPos1 = vec3(4. * sin(time * .5), 4., 6. * cos(time * .5));
    vec3 lightPos2 = vec3(-6., -3. * sin(time * .5), -1. * cos(time * .5));
    float diff = getLight(lightPos1 * d, p);
    color += diff * vec3(.2, .4, .8);
    color += diff * .5;
    diff = getLight(lightPos2 * d, p);
    color += diff * vec3(.8, .4, .2);
    color += diff * .4;
    color -= d * .002;
  } else {
    color += 1. - length(uv * .6);
    color *= 1. - cos(length(uv * (uv.x - uv.y)) * 20. - time);
    color = mix(color, vec3(0.), cos(length(uv) * 120. + time) * .5 + .5) * vec3(.3);
  }

  gl_FragColor = vec4(color, 1.);
}
  </script>

  <script src="twgl.min.js"></script>

  <script>
    const gl = document.querySelector("canvas").getContext("webgl");
    const programInfo = twgl.createProgramInfo(gl, ["vs", "fs"]);

    const arrays = {
      position: [-1, -1, 0, 1, -1, 0, -1, 1, 0, -1, 1, 0, 1, -1, 0, 1, 1, 0],
    };
    const bufferInfo = twgl.createBufferInfoFromArrays(gl, arrays);

    function render(time) {
      twgl.resizeCanvasToDisplaySize(gl.canvas);
      gl.viewport(0, 0, gl.canvas.width, gl.canvas.height);

      const uniforms = {
        time: time * 0.001,
        resolution: [gl.canvas.width, gl.canvas.height],
      };

      gl.useProgram(programInfo.program);
      twgl.setBuffersAndAttributes(gl, programInfo, bufferInfo);
      twgl.setUniforms(programInfo, uniforms);
      twgl.drawBufferInfo(gl, bufferInfo);

      requestAnimationFrame(render);
    }
    requestAnimationFrame(render);
  </script>
</body>
</html>
