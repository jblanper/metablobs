<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="icon" href="favicon.ico" type="image/x-icon" />      
  <title>Meta blobs - Blob #2</title>
  <link rel="stylesheet" href="blob.css" type="text/css">
</head>
<body>
  <div id="panel">
    <p>#2</p>
    <a href="index.html">
<svg width="35" height="35" version="1.1" viewBox="0 0 13.229166 13.229167" xmlns="http://www.w3.org/2000/svg"><g transform="translate(0 -283.77)"><path transform="matrix(.26458 0 0 .26458 0 283.77)" d="m25 5l-20 15v25h5v-23l15-11 15 11v18h-15v-10h5v-5h-10v15h-5v5h25 5v-25l-20-15z" style="paint-order:normal"/></g></svg>
    </a>
    <a href="https://github.com/jblanper" target="_blank">
<svg width="35" height="35" version="1.1" viewBox="0 0 13.229166 13.229167" xmlns="http://www.w3.org/2000/svg"><g transform="translate(0 -283.77)"><path d="m6.6208 286.68-1.2972 7.4083h1.3656l1.2968-7.4083z" style="paint-order:normal"/><path d="m4.433 287.69c-1.3008 0.75108-2.6017 1.502-3.9026 2.2531v0.58497c-0.013266 0.11065 0.018061 0.24834 0.004134 0.30954 1.2995 0.75025 2.599 1.5003 3.8985 2.2505v-1.4769c-0.70558-0.40737-1.4111-0.81477-2.1167-1.2222 0.70558-0.40737 1.4111-0.81478 2.1167-1.2221z" style="paint-order:normal"/><path d="m8.794 287.69c1.3008 0.75108 2.6017 1.502 3.9026 2.2531v0.58497c0.01327 0.11065-0.01806 0.24834-0.0041 0.30954-1.2995 0.75025-2.599 1.5003-3.8985 2.2505v-1.4769c0.70558-0.40737 1.4111-0.81477 2.1167-1.2222-0.70558-0.40737-1.4111-0.81478-2.1167-1.2221z" style="paint-order:normal"/></g></svg>
    </a>
  </div>
  <canvas></canvas>

  <script id="vs" type="notjs">
attribute vec4 position;

void main() {
  gl_Position = position;
}
  </script>

  <script id="fs" type="notjs">
precision mediump float;
uniform float time;
uniform vec2 resolution;

#define MAX_STEPS 255
#define MAX_DIST 100.
#define EPSILON .0001

mat2 rotate(float a) {
  return mat2(cos(a), -sin(a), sin(a), cos(a));
}

float sphereSDF (vec3 p, float r) {
  return length(p) - r;
}

float displacement(vec3 p, float n) {
  return sin(p.x * n) * sin(p.y * n) * sin(p.z * n);
}

float sceneSDF (vec3 p) {
  float s = sphereSDF(p - vec3(0.), .8) * .5;
  return s + displacement(p, 5. * (sin(time * .2) * .6 + .6)) * .2;
}

vec3 getNormal (vec3 p) {
  float d = sceneSDF(p);
  vec2 e = vec2(.01, 0.);

  return normalize(d - vec3(
    sceneSDF(p - e.xyy),
    sceneSDF(p - e.yxy),
    sceneSDF(p - e.yyx)));
}

float raymarch (vec3 ro, vec3 rd) {
  float depth = 0.;
  for (int i = 0; i < MAX_STEPS; i++) {
    float dist = sceneSDF(ro + rd * depth);
    if (dist < EPSILON) return depth;
    depth += dist;
    if (depth >= MAX_DIST) return 0.;
  }
  return 0.;
}

struct Material {
  float ambient;
  float diffuse;
  float specular;
};

float getLight (vec3 lightPos, vec3 p, vec3 rd, float lightOcclusion, Material material) {
  // https://www.shadertoy.com/view/ll2GW1
  vec3 light = normalize(lightPos - p);
  vec3 normal = getNormal(p);

  // phong reflection
  float ambient = clamp(.5 + .5 * normal.y, 0., 1.);
  float diffuse = clamp(dot(normal, light), 0., 1.);
  vec3 half_way = normalize(-rd + light);
  float specular = pow(clamp(dot(half_way, normal), 0.0, 1.0), 16.);

  return (ambient * material.ambient * lightOcclusion) +
   (diffuse * material.diffuse * lightOcclusion) +
   (diffuse * specular * material.specular * lightOcclusion);
}

vec3 getRayDir (vec2 uv, vec3 rayOrigin, vec3 lookat, float zoom) {
  // https://www.youtube.com/watch?v=PBxuVlp7nuM
  vec3 forward = normalize(lookat - rayOrigin);
  vec3 right = normalize(cross(vec3(0., 1., 0.), forward));
  vec3 up = cross(forward, right);
  vec3 center = rayOrigin + forward * zoom;
  vec3 intersection = center + uv.x * right + uv.y * up;
  return normalize(intersection - rayOrigin);
}

void main (void) {
  vec2 uv = (gl_FragCoord.xy / resolution.xy) * 2.0 - 1.0;
  uv.x *= resolution.x / resolution.y;

  // camera
  vec3 ro = vec3(0., 5., -5.);
  ro.xz *= rotate(time * .1);
  vec3 rd = getRayDir(uv, ro, vec3(0., 0., 0.), 5.);

  vec3 color = vec3(0.);
  float d = raymarch(ro, rd);
  vec3 p = ro + rd * d;

  if (d > 0.) {
    color += smoothstep(.3, .4, sin(length(p * d - p.z * p.x) * 30. + time)) + vec3(.1, .2, .8);
    vec3 lightPos = vec3(8., 8., -10.);
    float phong = getLight(lightPos, p, rd, 1., Material(.2, .7, 1.5));
    color *= phong * vec3(.5, .5, .7);
  } else {
    vec2 gv = fract(abs(uv) * 1.) - .5;
    vec2 id = floor(abs(uv) * 1.);
    float t = ((mod(id.x, 2.) == 0.) ? -1. : 1.) * time * .1;
    gv *= rotate(t);

    float m = smoothstep(.34, .32, max(abs(gv.x), abs(gv.y))) +
      (smoothstep(.5, 0., length(gv))*1.1);

    float squares = cos(max(abs(gv.x), abs(gv.y)) * (80. * (sin(time * .2) * .4 + 1.))) * .3;
    float diamonds = cos(max(abs(gv.x+gv.y), abs(gv.y-gv.x)) * 50. - time) * .6;

    // background
    color += smoothstep(.1, .9, diamonds)*vec3(.3);
    color += smoothstep(.1, .2, squares) * vec3(.4, .2, .8) * .5;

    color /= 1.5-length(uv) * 4.5;
  }

  // gamma correction
  // https://iquilezles.org/www/articles/outdoorslighting/outdoorslighting.htm
  color = pow(color, vec3(1.0/2.2));

  gl_FragColor = vec4(color, 1.);
}
  </script>

  <script src="twgl.min.js"></script>

  <script>
    const gl = document.querySelector("canvas").getContext("webgl");
    const programInfo = twgl.createProgramInfo(gl, ["vs", "fs"]);

    const arrays = {
      position: [-1, -1, 0, 1, -1, 0, -1, 1, 0, -1, 1, 0, 1, -1, 0, 1, 1, 0],
    };
    const bufferInfo = twgl.createBufferInfoFromArrays(gl, arrays);

    function render(time) {
      twgl.resizeCanvasToDisplaySize(gl.canvas);
      gl.viewport(0, 0, gl.canvas.width, gl.canvas.height);

      const uniforms = {
        time: time * 0.001,
        resolution: [gl.canvas.width, gl.canvas.height],
      };

      gl.useProgram(programInfo.program);
      twgl.setBuffersAndAttributes(gl, programInfo, bufferInfo);
      twgl.setUniforms(programInfo, uniforms);
      twgl.drawBufferInfo(gl, bufferInfo);

      requestAnimationFrame(render);
    }
    requestAnimationFrame(render);
  </script>
</body>
</html>
